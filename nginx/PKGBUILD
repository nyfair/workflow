# Maintainer: nyfair <nyfair2012@gmail.com>
pkgname=nginx
pkgver=1.29.5
pkgrel=1
pkgdesc='Lightweight HTTP server'
arch=('x86_64')
url='https://nginx.org/'
license=('custom')
source=("https://github.com/nginx/nginx/archive/release-$pkgver.tar.gz")
md5sums=('SKIP')

build() {
  cd $srcdir/nginx-release-$pkgver
  sed -i '/ -g/d' auto/cc/gcc
  sed -i '/-Werror/d' auto/cc/gcc
  sed -i 's/NGX_PLATFORM=win32/# NGX_PLATFORM=win32/' auto/configure
  auto/configure \
    --prefix=/etc/nginx \
    --conf-path=/etc/nginx/nginx.conf \
    --sbin-path=/usr/bin/nginx.exe \
    --pid-path=/tmp/nginx.pid \
    --http-log-path=/tmp/nginx_access.log \
    --error-log-path=/tmp/nginx_error.log \
    --http-client-body-temp-path=/tmp/nginx_clientbody \
    --http-proxy-temp-path=/tmp/nginx_proxy \
    --with-cc-opt="$CFLAGS -DFD_SETSIZE=65536" \
    --with-openssl-opt=enable-ktls --with-http_ssl_module --with-http_v2_module --with-http_v3_module \
    --without-http_charset_module --without-http_ssi_module --without-http_userid_module \
    --without-http_access_module --without-http_auth_basic_module --without-http_mirror_module \
    --without-http_autoindex_module --without-http_geo_module --without-http_map_module \
    --without-http_split_clients_module --without-http_rewrite_module --without-http_fastcgi_module \
    --without-http_uwsgi_module --without-http_scgi_module \
    --without-http_grpc_module --without-http_memcached_module --without-http_limit_conn_module \
    --without-http_limit_req_module --without-http_empty_gif_module --without-http_browser_module \
    --without-http_upstream_hash_module --without-http_upstream_ip_hash_module --without-http_upstream_least_conn_module \
    --without-http_upstream_keepalive_module --without-http_upstream_zone_module \
    --with-stream
  make
}

package() {
  cd $srcdir/nginx-release-$pkgver
  make DESTDIR=$pkgdir install
  rm -rf $pkgdir/etc/nginx/*
}
